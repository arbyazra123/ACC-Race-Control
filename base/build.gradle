plugins{
    id 'java'
    id 'jacoco'
    id 'application'
    id 'maven'
}

apply from: "${rootDir}/gradle/versioning.gradle"
apply from: "${rootDir}/gradle/license-header.gradle"
def distributionName = "ACC Race Control " + project.ext.versionString


application{
    mainClass = 'racecontrol.Main'
}

repositories {
    jcenter()
    mavenCentral()
}

sourceSets{
    main{
        resources{
            srcDirs "src/main/resources", "../changelog"
        }
    }
}

compileJava.options.encoding = 'ISO-8859-1'

sourceCompatibility = '11'
targetCompatibility = '11'

dependencies {
    testImplementation     'junit:junit:4.13'

    // Apple macOS eawt stub (needed by Processing 3.3.6 on non-Apple JDK 17+)
    runtimeOnly files('libs/apple-eawt-stub.jar')

    //compile group: 'org.processing', name: 'core', version: '3.3.6'
    implementation 'org.processing:core:3.3.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.12.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.12.0'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20200707-1.30.10'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.30.4'
    implementation 'com.google.api-client:google-api-client:1.30.4'
}

run{
    dependsOn installDist
    workingDir new File(new File(buildDir, 'install'),distributionName)
    jvmArgs("-splash:data/loading.png")
    // Processing 3.3.6 uses com.apple.eawt.QuitHandler which was removed in Java 9+.
    // Patch java.desktop with the missing Apple eawt classes from the stub jar.
    jvmArgs("--patch-module", "java.desktop=${file('libs/apple-eawt-stub.jar').absolutePath}")
    jvmArgs("--add-exports", "java.desktop/com.apple.eawt=ALL-UNNAMED")
    jvmArgs("--add-opens",   "java.desktop/com.apple.eawt=ALL-UNNAMED")
}

distributions{
    main{
        distributionBaseName = distributionName
        contents {
            into("/Google Sheets API Key"){
                from "credentials"
            }
        }
    }
}

configure(install.repositories.mavenInstaller){
    pom.project{
        groupId = 'org.leonard-schuengel'
        artifactId = 'acc-race-control'
        version = '1.0'
    }
}

task buildMacDmg {
    group = "Release"
    description = "Builds a macOS .app bundle and .dmg using jpackage (requires Zulu 17)"
    dependsOn installDist

    doLast {
        def distDir    = new File(new File(buildDir, 'install'), distributionName)
        def stagingDir = new File(buildDir, 'jpackage-input')
        def outputDir  = new File(buildDir, 'dmg')
        def jpackage   = '/Library/Java/JavaVirtualMachines/zulu-17.jdk/Contents/Home/bin/jpackage'
        def runtimeImg = '/Library/Java/JavaVirtualMachines/zulu-11.jdk/Contents/Home'
        def iconFile   = "${rootDir}/favicon/AppIcon.icns"

        // Stage: all jars + data folder in one flat input dir
        delete stagingDir
        stagingDir.mkdirs()
        outputDir.mkdirs()

        copy {
            from new File(distDir, 'lib')
            into stagingDir
        }
        copy {
            from new File(distDir, 'data')
            into new File(stagingDir, 'data')
        }

        exec {
            commandLine jpackage,
                '--type',          'dmg',
                '--name',          'ACC Race Control',
                '--app-version',   project.ext.versionString,
                '--input',         stagingDir.absolutePath,
                '--main-jar',      'base.jar',
                '--main-class',    'racecontrol.Main',
                // Set working dir inside app bundle so relative data/ paths resolve correctly
                '--java-options',  '-Duser.dir=$APPDIR',
                '--java-options',  '-splash:$APPDIR/data/loading.png',
                // Processing 3.3.6 needs com.apple.eawt.QuitHandler; patch it into java.desktop
                '--java-options',  '--patch-module java.desktop=$APPDIR/apple-eawt-stub.jar',
                '--java-options',  '--add-exports java.desktop/com.apple.eawt=ALL-UNNAMED',
                '--java-options',  '--add-opens java.desktop/com.apple.eawt=ALL-UNNAMED',
                '--runtime-image', runtimeImg,
                '--icon',          iconFile,
                '--dest',          outputDir.absolutePath
        }

        println "DMG created in: ${outputDir.absolutePath}"
    }
}

task copyLibs(type:Copy){
    group = "Release"
    from configurations.runtimeClasspath
    from jar
    into "$projectDir/build/release/" + distributionName + "/lib"
}
task copyDist(type:Copy){
    group = "Release"
    from "$projectDir/src/dist"
    into "$projectDir/build/release/" + distributionName
}

task buildRelease(type: Zip){
    group = "Release"
    dependsOn copyLibs
    dependsOn copyDist
    dependsOn install
    from "$projectDir/build/release/" + distributionName
    archiveName distributionName + ".zip"
    destinationDir(file("$projectDir/build/release/"))
}



/*
compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}
*/


